---
page_title: "microsoft365_graph_beta_agents_agent_identity_blueprint_certificate_credential Resource - terraform-provider-microsoft365"
subcategory: "Agents"

description: |-
  Manages a certificate credential for an Agent Identity Blueprint application using the Microsoft Graph Beta API. This resource uses PATCH on the application's keyCredentials property with OData type cast to microsoft.graph.agentIdentityBlueprint.
---

# microsoft365_graph_beta_agents_agent_identity_blueprint_certificate_credential (Resource)

Manages a certificate credential for an Agent Identity Blueprint application using the Microsoft Graph Beta API. This resource uses PATCH on the application's keyCredentials property with OData type cast to microsoft.graph.agentIdentityBlueprint.

## Microsoft Documentation

- [keyCredential resource type](https://learn.microsoft.com/en-us/graph/api/resources/keycredential?view=graph-rest-beta)
- [Agent Identity Blueprint API Overview](https://learn.microsoft.com/en-us/graph/api/resources/agentid-platform-overview?view=graph-rest-beta)

## API Permissions

The following API permissions are required in order to use this resource.

### Microsoft Graph

- **Read Permissions**: `AgentIdentityBlueprint.Read.All`, `Application.Read.All`, `Directory.Read.All`
- **Write Permissions**: `AgentIdentityBlueprint.ReadWrite.All`, `Directory.ReadWrite.All`

Find out more about the permissions required for managing agent identities at Microsoft Learn [here](https://learn.microsoft.com/en-us/graph/api/resources/agentid-platform-overview?view=graph-rest-beta#permissions-for-managing-agent-identities).

## Version History

| Version | Status | Notes |
|---------|--------|-------|
| v0.38.0 | Experimental | Initial release |

## Important Notes

- **The `key` attribute is write-only.** The certificate content cannot be retrieved from the API after creation.
- This resource does not support import because the key value cannot be retrieved from the API after creation.
- Unlike the standard application keyCredentials endpoint which supports both `AsymmetricX509Cert` and `Symmetric`, the Agent Identity Blueprint API only supports `AsymmetricX509Cert` (X.509 certificates).
- The `key_id` is generated by the API and cannot be specified during creation.
- When `replace_existing_certificates` is `true`, all existing certificates on the blueprint will be replaced.

## Certificate Encoding

The `encoding` attribute specifies how the certificate data in `key` is encoded:

| Encoding | Description | Terraform Usage |
|----------|-------------|-----------------|
| `pem` (default) | PEM format with `-----BEGIN CERTIFICATE-----` headers | `file("cert.pem")` |
| `base64` | Base64-encoded DER (binary) format | `filebase64("cert.der")` |
| `hex` | Hex-encoded certificate | Pre-generated hex string |

## Example Usage

### Certificate with PEM Encoding (Default)

```terraform
# Example: Create a certificate credential with PEM encoding (default)

# First, create or reference an existing agent identity blueprint
resource "microsoft365_graph_beta_agents_agent_identity_blueprint" "example" {
  display_name     = "my-agent-blueprint"
  sponsor_user_ids = ["00000000-0000-0000-0000-000000000000"]
  owner_user_ids   = ["00000000-0000-0000-0000-000000000000"]
  description      = "Agent identity blueprint for certificate authentication"
}

# Generate a self-signed certificate using the tls provider
resource "tls_private_key" "example" {
  algorithm = "RSA"
  rsa_bits  = 2048
}

resource "tls_self_signed_cert" "example" {
  private_key_pem = tls_private_key.example.private_key_pem

  subject {
    common_name  = "my-agent-certificate"
    organization = "My Organization"
  }

  validity_period_hours = 8760 # 1 year

  allowed_uses = [
    "key_encipherment",
    "digital_signature",
    "client_auth",
  ]
}

# Create a certificate credential using PEM encoding
resource "microsoft365_graph_beta_agents_agent_identity_blueprint_certificate_credential" "example" {
  blueprint_id = microsoft365_graph_beta_agents_agent_identity_blueprint.example.id
  display_name = "my-agent-certificate"

  key      = tls_self_signed_cert.example.cert_pem
  encoding = "pem" # Default, can be omitted
  type     = "AsymmetricX509Cert"
  usage    = "Verify"
}

output "certificate_key_id" {
  value       = microsoft365_graph_beta_agents_agent_identity_blueprint_certificate_credential.example.key_id
  description = "The key ID of the certificate credential"
}

output "certificate_thumbprint" {
  value       = microsoft365_graph_beta_agents_agent_identity_blueprint_certificate_credential.example.thumbprint
  description = "The thumbprint (SHA-1 hash) of the certificate"
}
```

### Certificate with DER/Base64 Encoding

```terraform
# Example: Create a certificate credential with DER/Base64 encoding
# Use this when you have a DER (binary) certificate file

# First, create or reference an existing agent identity blueprint
resource "microsoft365_graph_beta_agents_agent_identity_blueprint" "example_der" {
  display_name     = "my-agent-blueprint-der"
  sponsor_user_ids = ["00000000-0000-0000-0000-000000000000"]
  owner_user_ids   = ["00000000-0000-0000-0000-000000000000"]
  description      = "Agent identity blueprint for DER certificate authentication"
}

# Option 1: Load a DER certificate from file using filebase64()
resource "microsoft365_graph_beta_agents_agent_identity_blueprint_certificate_credential" "from_file" {
  blueprint_id = microsoft365_graph_beta_agents_agent_identity_blueprint.example_der.id
  display_name = "my-der-certificate"

  key      = filebase64("path/to/certificate.der")
  encoding = "base64"
  type     = "AsymmetricX509Cert"
  usage    = "Verify"
}

# Option 2: Generate a certificate and convert PEM to base64 (DER equivalent)
resource "tls_private_key" "example_der" {
  algorithm = "RSA"
  rsa_bits  = 2048
}

resource "tls_self_signed_cert" "example_der" {
  private_key_pem = tls_private_key.example_der.private_key_pem

  subject {
    common_name  = "my-agent-certificate-der"
    organization = "My Organization"
  }

  validity_period_hours = 8760 # 1 year

  allowed_uses = [
    "key_encipherment",
    "digital_signature",
    "client_auth",
  ]
}

# Extract the base64 content from PEM (strip headers) - equivalent to base64-encoded DER
locals {
  cert_base64 = replace(
    replace(
      replace(tls_self_signed_cert.example_der.cert_pem, "-----BEGIN CERTIFICATE-----", ""),
      "-----END CERTIFICATE-----", ""
    ),
    "\n", ""
  )
}

resource "microsoft365_graph_beta_agents_agent_identity_blueprint_certificate_credential" "from_generated" {
  blueprint_id = microsoft365_graph_beta_agents_agent_identity_blueprint.example_der.id
  display_name = "my-generated-der-certificate"

  key      = local.cert_base64
  encoding = "base64"
  type     = "AsymmetricX509Cert"
  usage    = "Verify"
}
```

### Certificate with HEX Encoding

```terraform
# Example: Create a certificate credential with HEX encoding
# Use this when you have a hex-encoded certificate string

# First, create or reference an existing agent identity blueprint
resource "microsoft365_graph_beta_agents_agent_identity_blueprint" "example_hex" {
  display_name     = "my-agent-blueprint-hex"
  sponsor_user_ids = ["00000000-0000-0000-0000-000000000000"]
  owner_user_ids   = ["00000000-0000-0000-0000-000000000000"]
  description      = "Agent identity blueprint for HEX certificate authentication"
}

# Create a certificate credential using a pre-generated hex-encoded certificate
# To generate a hex-encoded certificate:
#   1. openssl x509 -in cert.pem -outform DER -out cert.der
#   2. xxd -p cert.der | tr -d '\n' > cert.hex
resource "microsoft365_graph_beta_agents_agent_identity_blueprint_certificate_credential" "example_hex" {
  blueprint_id = microsoft365_graph_beta_agents_agent_identity_blueprint.example_hex.id
  display_name = "my-hex-certificate"

  # Pre-generated hex-encoded certificate string
  # Generate with: openssl x509 -in cert.pem -outform DER | xxd -p | tr -d '\n'
  key      = file("path/to/certificate.hex")
  encoding = "hex"
  type     = "AsymmetricX509Cert"
  usage    = "Verify"
}

output "hex_certificate_key_id" {
  value       = microsoft365_graph_beta_agents_agent_identity_blueprint_certificate_credential.example_hex.key_id
  description = "The key ID of the hex-encoded certificate credential"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `blueprint_id` (String) The unique identifier of the agent identity blueprint application.
- `key` (String, Sensitive) The certificate data. The format depends on the `encoding` attribute: for `pem`, use `file("path/to/cert.pem")`; for `base64`, use `filebase64("path/to/cert.der")` or a base64 string; for `hex`, provide a hex-encoded string.

### Optional

- `display_name` (String) Friendly name for the certificate. Optional.
- `encoding` (String) Specifies the encoding used for the `key` value. Possible values are `pem`, `base64`, or `hex`. Defaults to `pem`.
- `end_date_time` (String) The date and time at which the credential expires. The timestamp type represents date and time information using ISO 8601 format and is always in UTC. For example, midnight UTC on Jan 1, 2025 is 2025-01-01T00:00:00Z. Must be in the future.
- `replace_existing_certificates` (Boolean) When `true`, replaces all existing certificates on the application. When `false` (default), preserves existing certificates and adds the new one.
- `start_date_time` (String) The date and time at which the credential becomes valid. The timestamp type represents date and time information using ISO 8601 format and is always in UTC. For example, midnight UTC on Jan 1, 2024 is 2024-01-01T00:00:00Z. If not specified, defaults to the current time.
- `timeouts` (Attributes) (see [below for nested schema](#nestedatt--timeouts))
- `type` (String) The type of key credential. Unlike the standard application keyCredentials endpoint which supports both `AsymmetricX509Cert` and `Symmetric`, the Agent Identity Blueprint API only supports `AsymmetricX509Cert` (X.509 certificates).
- `usage` (String) A string that describes the purpose for which the key can be used. Must be `Verify`.

### Read-Only

- `custom_key_identifier` (String) A 40-character binary type that can be used to identify the credential. Optional. When not provided in the payload, defaults to the thumbprint of the certificate.
- `key_id` (String) The unique identifier (GUID) for the key credential.
- `thumbprint` (String) The thumbprint (SHA-1 hash) of the certificate.

<a id="nestedatt--timeouts"></a>
### Nested Schema for `timeouts`

Optional:

- `create` (String) A string that can be [parsed as a duration](https://pkg.go.dev/time#ParseDuration) consisting of numbers and unit suffixes, such as "30s" or "2h45m". Valid time units are "s" (seconds), "m" (minutes), "h" (hours).
- `delete` (String) A string that can be [parsed as a duration](https://pkg.go.dev/time#ParseDuration) consisting of numbers and unit suffixes, such as "30s" or "2h45m". Valid time units are "s" (seconds), "m" (minutes), "h" (hours). Setting a timeout for a Delete operation is only applicable if changes are saved into state before the destroy operation occurs.
- `read` (String) A string that can be [parsed as a duration](https://pkg.go.dev/time#ParseDuration) consisting of numbers and unit suffixes, such as "30s" or "2h45m". Valid time units are "s" (seconds), "m" (minutes), "h" (hours). Read operations occur during any refresh or planning operation when refresh is enabled.
- `update` (String) A string that can be [parsed as a duration](https://pkg.go.dev/time#ParseDuration) consisting of numbers and unit suffixes, such as "30s" or "2h45m". Valid time units are "s" (seconds), "m" (minutes), "h" (hours).

## Import

~> **Note:** Import is NOT supported for this resource because the `key` value is only available at creation time and cannot be retrieved from the API.

